PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT
  ?dish
  ?labelEN
  ?image
  (GROUP_CONCAT(DISTINCT ?originQ;  separator="|") AS ?origins)
  (GROUP_CONCAT(DISTINCT ?cuisineQ; separator="|") AS ?cuisines)
  (GROUP_CONCAT(DISTINCT ?tagQ;     separator="|") AS ?tags)
WHERE {
  # ==== 取り込み対象 ====
  ${FILTER}

  # 英語ラベル
  ?dish rdfs:label ?labelEN .
  FILTER (LANG(?labelEN) = "en")

  # 画像
  OPTIONAL { ?dish wdt:P18 ?image . }

  # 起源 / cuisine（QIDだけ拾う）
  OPTIONAL { ?dish wdt:P495 ?origin . BIND( STRAFTER(STR(?origin), "entity/") AS ?originQ ) }
  OPTIONAL { ?dish wdt:P2012 ?cui    . BIND( STRAFTER(STR(?cui),    "entity/") AS ?cuisineQ ) }

  # タグ（祖先 + 自分自身をQIDで）
  {
    { BIND(?dish AS ?ancAll) }
    UNION
    { ?dish (wdt:P31|wdt:P279|wdt:P361)+ ?ancAll }
  }
  BIND( STRAFTER(STR(?ancAll), "entity/") AS ?tagQ )
}
GROUP BY ?dish ?labelEN ?image
ORDER BY ?dish
