# dishes_with_tags.rq (改)
SELECT
  ?dish
  ?labelEN
  ?image
  (GROUP_CONCAT(DISTINCT ?originQ;   separator="|") AS ?origins)
  (GROUP_CONCAT(DISTINCT ?cuisineQ;  separator="|") AS ?cuisines)
  (GROUP_CONCAT(DISTINCT ?ancestorQ; separator="|") AS ?tags)
WHERE {
  # 料理アイテム
  {
    ?dish wdt:P31/wdt:P279* wd:Q746549.
  }
  UNION
  {
    ?dish wdt:P279+ wd:Q746549.
  }

  # 英語ラベル
  ?dish rdfs:label ?labelEN.
  FILTER (LANG(?labelEN) = "en")

  # 画像
  OPTIONAL { ?dish wdt:P18 ?image. }

  # 起源（QID 文字列化）
  OPTIONAL {
    ?dish wdt:P495 ?origin .
    BIND( STRAFTER(STR(?origin), "entity/") AS ?originQ )
  }

  # cuisine（QID 文字列化）
  OPTIONAL {
    ?dish wdt:P2012 ?cui .
    BIND( STRAFTER(STR(?cui), "entity/") AS ?cuisineQ )
  }

  # タグ（祖先 + 自分自身を QID で）
  OPTIONAL {
    {
      ?dish (wdt:P31|wdt:P279|wdt:P361)+ ?ancestor .
      BIND( STRAFTER(STR(?ancestor), "entity/") AS ?ancestorQ )
    }
    UNION
    {
      BIND( STRAFTER(STR(?dish), "entity/") AS ?ancestorQ )
    }
  }
}
GROUP BY ?dish ?labelEN ?image
ORDER BY ?dish
